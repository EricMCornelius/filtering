<html>
<head>
<title>Filterer</title>
<style>

canvas {
  width: 100%;
  height: 100%
}

html, body {
  width:  100%;
  height: 100%;
  margin: 0px;
}

#control_bar {
  position: fixed;
  bottom: 0px;
  width:  100%;
  height: 10px;
  background-color: transparent;
  transition: height 1s;
}

#control_bar:hover {
  height: 50px;
  padding: 0px;
}

.button {
  display: inline-block;
  height: 100%;
  padding-left: 5px;
  padding-right: 5px;
  bottom: 0px;
  background-color: transparent;

  font-weight: bold;
  font-size: 2em;
  font-family: 'arial';
  text-align: center;
  vertical-align: middle;
  line-height: 50px;
}

.noselect {
  -webkit-touch-callout: none;
  -webkit-user-select: none;
  -khtml-user-select: none;
  -moz-user-select: none;
  -ms-user-select: none;
  user-select: none;
}

.button:hover {
  cursor: pointer;
}

</style>

<script id="color_template" type="x-css-template">
  .{{color}} {
    color: {{primary}};
    text-shadow: 0 0 5px {{primary}}, 0 0 10px {{primary}}, 0 0 15px {{primary}}, 0 0 20px {{secondary}}, 0 0 50px {{secondary}}, 0 0 70px {{secondary}}, 0 0 100px {{secondary}};
    transition: text-shadow 0.25s, color 0.25s;
  }

  .{{color}}:hover {
    color: {{primary}};
    text-shadow: 0 0 5px {{primary}}, 0 0 10px {{primary}}, 0 0 15px {{primary}}, 0 0 20px {{secondary}}, 0 0 50px {{secondary}}, 0 0 60px {{secondary}}, 0 0 70px {{secondary}}, 0 0 90px {{secondary}}, 0 0 100px {{secondary}}, 0 0 120px {{secondary}};
  }
</script>

<script type='text/javascript'>
  var colors = [
    {color: 'red', primary: '#ff55aa', secondary: '#de0000'},
    {color: 'blue', primary: '#55aaff', secondary: '#0000de'},
    {color: 'green', primary: '#55ffaa', secondary: '#00de00'},
    {color: 'white', primary: '#ffffff', secondary: '#dddddd'}
  ];

  function replace(tmpl, args) {
    for (var key in args) {
      var value = args[key];
      tmpl = tmpl.replace(new RegExp('{{' + key + '}}', 'g'), value);
    }
    return tmpl;
  }

  colors.forEach(function(color) {
    var template = document.getElementById('color_template').textContent;
    var style_str = replace(template, color);

    var sheet = document.createElement('style');
    sheet.innerHTML = style_str;
    document.head.appendChild(sheet);
  });
</script>

<script id="blur_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 sum = vec4(0.0);

    sum += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y - 1.0 * height)) * 1.0;
    sum += texture2D(tDiffuse, vec2(vUv.x,               vUv.y - 1.0 * height)) * 2.0;
    sum += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y - 1.0 * height)) * 1.0;
    sum += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y               )) * 2.0;
    sum += texture2D(tDiffuse, vec2(vUv.x,               vUv.y               )) * 4.0;
    sum += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y               )) * 2.0;
    sum += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y + 1.0 * height)) * 1.0;
    sum += texture2D(tDiffuse, vec2(vUv.x,               vUv.y + 1.0 * height)) * 2.0;
    sum += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y + 1.0 * height)) * 1.0;

    sum /= 16.0;
    //sum -= 0.005;
    gl_FragColor = vec4(sum.rgb, opacity);
  }
</script>

<script id="fade_out_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float width;
  uniform float height;

  void main() {
    vec4 color = texture2D(tDiffuse, vec2(vUv.x, vUv.y));
    color -= 0.005;
    gl_FragColor = color;
  }
</script>

<script id="edge_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 sum_x = vec4(0.0);
    vec4 sum_y = vec4(0.0);

    sum_x += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y - 1.0 * height)) * -1.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y               )) * -2.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y + 1.0 * height)) * -1.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y - 1.0 * height)) *  1.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y               )) *  2.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y + 1.0 * height)) *  1.0;

    sum_y += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y - 1.0 * height)) * -1.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x              , vUv.y - 1.0 * height)) * -2.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y - 1.0 * height)) * -1.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y + 1.0 * height)) *  1.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x              , vUv.y + 1.0 * height)) *  2.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y + 1.0 * height)) *  1.0;

    vec4 sum = sqrt(sum_x * sum_x + sum_y * sum_y);
    gl_FragColor = vec4(sum.rgb, opacity);
  }
</script>

<script id="toon_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 sum_x = vec4(0.0);
    vec4 sum_y = vec4(0.0);

    sum_x += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y - 1.0 * height)) * -1.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y               )) * -2.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y + 1.0 * height)) * -1.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y - 1.0 * height)) *  1.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y               )) *  2.0;
    sum_x += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y + 1.0 * height)) *  1.0;

    sum_y += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y - 1.0 * height)) * -1.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x              , vUv.y - 1.0 * height)) * -2.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y - 1.0 * height)) * -1.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x - 1.0 * width, vUv.y + 1.0 * height)) *  1.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x              , vUv.y + 1.0 * height)) *  2.0;
    sum_y += texture2D(tDiffuse, vec2(vUv.x + 1.0 * width, vUv.y + 1.0 * height)) *  1.0;

    vec4 sum = sqrt(sum_x * sum_x + sum_y * sum_y);
    float magnitude = length(sum);
    if (magnitude > 0.5) {
      gl_FragColor = vec4(0.0, 0.0, 0.0, opacity);
    }
    else {
      vec4 color = texture2D(tDiffuse, vUv);
      color = color - mod(color, 0.1);
      gl_FragColor = vec4(color.rgb, opacity);
    }
  }
</script>

<script id="invert_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float width;
  uniform float height;

  void main() {
    vec4 color = texture2D(tDiffuse, vUv);
    vec4 inverted = vec4(1.0 - color.r, 1.0 - color.g, 1.0 - color.b, 1.0);
    gl_FragColor = inverted;
  }
</script>

<script id="red_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 color = texture2D(tDiffuse, vUv);
    gl_FragColor = vec4(color.r, 0.0, 0.0, opacity);
  }
</script>

<script id="green_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 color = texture2D(tDiffuse, vUv);
    gl_FragColor = vec4(0.0, color.g, 0.0, opacity);
  }
</script>

<script id="blue_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 color = texture2D(tDiffuse, vUv);
    gl_FragColor = vec4(0.0, 0.0, color.b, opacity);
  }
</script>

<script id="motion_blur_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float opacity;
  uniform float width;
  uniform float height;

  void main() {
    vec4 color = texture2D(tDiffuse, vUv);
    gl_FragColor = vec4(color.rgb, opacity);
  }
</script>

<script id="static_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float time;
  uniform float opacity;
  uniform float width;
  uniform float height;

  float rand(vec2 co) {
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
  }

  void main() {
    float val = rand(time * gl_FragCoord.xy);
    gl_FragColor = vec4(val, val, val, opacity);
  }
</script>

<script id="julia_shader" type="x-shader/x-fragment">
  varying vec2 vUv;
  uniform sampler2D tDiffuse;

  uniform float time;
  uniform float opacity;
  uniform float width;
  uniform float height;

  float rand(vec2 co) {
    return fract(sin(dot(co.xy, vec2(12.9898,78.233))) * 43758.5453);
  }

  void main() {
    float val = rand(time * gl_FragCoord.xy);
    gl_FragColor = vec4(val, 0.0, 0.0, 1.0);
  }
</script>

<script id="perlin_noise_shader" type="x-shader/x-fragment">
// https://github.com/ashima/webgl-noise/blob/master/src
uniform float time;
uniform float opacity;
uniform float width;
uniform float height;

//
// Description : Array and textureless GLSL 2D/3D/4D simplex
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
     return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v)
  {
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

// Permutations
  i = mod289(i);
  vec4 p = permute( permute( permute(
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients: 7x7 points over a square, mapped onto an octahedron.
// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
                                dot(p2,x2), dot(p3,x3) ) );
  }

const int num_iters = 10;
float getVal(float factor, vec2 coords) {
  float val = 0.0;
  float persistence = 0.75;
  float sum = 0.0;
  for (int x = 0; x <= num_iters; ++x) {
    float freq = pow(2.0, float(x));
    float amp = pow(persistence, float(x));
    val += amp * snoise(vec3(coords.x * width * freq, coords.y * height * freq, factor));
    sum += amp;
  }
  return 0.5 * ((val / sum) + 1.0);
}

float getVal2(float factor, vec2 coords) {
  float val = 0.3;
  for (int x = 1; x <= num_iters; ++x) {
    float size = pow(2.0, float(x));
    val = 0.7 * val + 0.3 * snoise(vec3(coords.x * width * size, coords.y * height * size, factor));
  }
  return val;
}

void main() {
  float factor = time / 2.0;
  float r = getVal(factor, gl_FragCoord.xy);
  float g = getVal(factor + 2.0, gl_FragCoord.xy);
  //float b = getVal(factor + 3.0, gl_FragCoord.xy);
  gl_FragColor = vec4(r, g, 0.0, opacity);
}
</script>

<script id="mandelbrot_shader" type="x-shader/x-fragment">
  uniform float time;
  uniform float opacity;
  uniform float width;
  uniform float height;

  #define NUM_STEPS 512

  precision highp float;
  precision highp int;

  // http://lolengine.net/blog/2013/07/27/rgb-to-hsv-in-glsl
  vec3 rgb2hsv(vec3 c)
  {
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
  }

  vec3 hsv2rgb(vec3 c)
  {
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
  }

  void main() {
    vec2 z;
    float tmp;
    int steps;
    float val = 0.0;
    float zoom = 1.0 / pow(1.3, time);
    float x0 = 2.0 * (width * gl_FragCoord.x - 0.5) * zoom * (height / width);
    float y0 = 2.0 * (height * gl_FragCoord.y - 0.5) * zoom;

    // http://www.nahee.com/Derbyshire/manguide.html

    // seahorse valley
    x0 -= 0.746;
    y0 += 0.101;

    // elephant valley
    // x0 += 0.275;

    // buzzsaw
    // x0 += 0.001643721971153;
    // y0 -= 0.822467633298876;

    z.x = x0;
    z.y = y0;

    float bailout = pow(2.0, 8.0);
    for (int step = 0; step < NUM_STEPS; ++step) {
      steps = step;

      tmp = z.x * z.x - z.y * z.y + x0;
      z.y = 2.0 * z.y * z.x + y0;
      z.x = tmp;
      if (z.x * z.x + z.y * z.y >= bailout)
        break;
    }

    float v = 0.2 * float(steps) / float(NUM_STEPS);
    // v += (steps != NUM_STEPS - 1) ? log2(log2(length(z))) / float(NUM_STEPS) : 0.0;
    // v += (steps == NUM_STEPS - 1) ? 4.0 - (z.x * z.x + z.y * z.y) / 10.0 : 0.0;
    vec3 c = hsv2rgb(vec3(v, 1.0, 1.0));
    gl_FragColor = vec4(c, opacity);
  }
</script>

<script id="vertex_shader_screen" type="x-shader/x-vertex">

  varying vec2 vUv;

  void main() {
    vUv = uv;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
  }

</script>


</head>
<body>
<div id='render_target' class='noselect droppable'></div>
<div id='control_bar' class='noselect'>
  <span class='noselect red button' data-filter='blur'>Blur</span>
  <span class='noselect green button' data-filter='toon'>Toon</span>
  <span class='noselect blue button' data-filter='edge'>Edge</span>
  <span class='noselect red button' data-filter='red'>Red</span>
  <span class='noselect green button' data-filter='green'>Green</span>
  <span class='noselect blue button' data-filter='blue'>Blue</span>
  <span class='noselect white button' data-filter='static'>Static</span>
  <span class='noselect white button' data-filter='julia'>Julia</span>
  <span class='noselect white button' data-filter='mandelbrot'>Mandelbrot</span>
  <span class='noselect white button' data-filter='perlin'>Perlin</span>
</div>

<script type='text/javascript' src='//cdnjs.cloudflare.com/ajax/libs/three.js/r68/three.js'></script>
<script type='text/javascript' src='//cdnjs.cloudflare.com/ajax/libs/d3/3.4.13/d3.min.js'></script>
<script type='text/javascript' src='js/main.js'></script>
<script type='text/javascript' src='js/droppable.js'></script>
</body>
<html>
